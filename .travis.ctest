string(SUBSTRING "$ENV{TRAVIS_COMMIT}" 0 7 COMMIT_ID)
set(CTEST_SITE "travis")
set(CTEST_BUILD_NAME "$ENV{TRAVIS_BUILD_NUMBER}-${COMMIT_ID}-$ENV{CC}-$ENV{CC_VERSION}")

set(CTEST_SOURCE_DIRECTORY "$ENV{TRAVIS_BUILD_DIR}")
set(CTEST_BINARY_DIRECTORY "${CTEST_SOURCE_DIRECTORY}/build")

configure_file(${CTEST_SOURCE_DIRECTORY}/.travis.ctest.note.in ${CTEST_BINARY_DIRECTORY}/.travis.ctest.note)
set(CTEST_NOTES_FILES ${CTEST_BINARY_DIRECTORY}/.travis.ctest.note)

set(CTEST_CMAKE_GENERATOR "Unix Makefiles")
set(CTEST_BUILD_CONFIGURATION "$ENV{CMAKE_BUILD_TYPE}")
set(CTEST_CONFIGURATION_TYPE "Debug")

find_program(CTEST_MEMORYCHECK_COMMAND valgrind)
set(CTEST_MEMORYCHECK_COMMAND_OPTIONS "--track-origins=yes --leak-check=yes")

find_program(CTEST_COVERAGE_COMMAND "gcov-$ENV{GCC_VERSION}")
set(CTEST_CUSTOM_COVERAGE_EXCLUDE ${CTEST_CUSTOM_COVERAGE_EXCLUDE} "/distribution/" "/examples/" "/sourcecode/thirdparty/" "/tests/" "/cov-int/" "/CMakeFiles/" "/usr/")
set(COVERAGE_EXTRA_FLAGS "-l -p")

ctest_start(Continuous)
ctest_configure(OPTIONS "-DKD_BUILD_CI=On")
ctest_build()
ctest_test()
if(CTEST_MEMORYCHECK_COMMAND)
    ctest_memcheck()
endif()
if(CTEST_COVERAGE_COMMAND)
    ctest_coverage()
endif()
ctest_submit()
